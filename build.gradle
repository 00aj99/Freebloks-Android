buildscript {
    ext.kotlin_version = '1.3.61'
    repositories {
        jcenter()
        google()
        maven {
            url 'https://maven.fabric.io/public'
        }
    }

    dependencies {
        // https://developer.android.com/studio/releases/gradle-plugin
        classpath 'com.android.tools.build:gradle:3.5.3'

        // kotlin
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        // https://developers.google.com/android/guides/releases
        classpath 'com.google.gms:google-services:4.3.3'

        // https://docs.fabric.io/android/changelog.html
        classpath 'io.fabric.tools:gradle:1.31.2'
    }
}

repositories {
    jcenter()
    maven {
        url "https://maven.google.com"
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // https://github.com/Clans/FloatingActionButton
    implementation 'com.github.clans:fab:1.6.4'

    // for Google+ integration. See "foss" branch without this dependency
    // https://developers.google.com/android/guides/releases
    implementation 'com.google.android.gms:play-services-games:19.0.0'
    implementation 'com.google.android.gms:play-services-auth:17.0.0'

    // for crash reporting through firebase. See "foss" branch without this dependency.
    implementation('com.crashlytics.sdk.android:crashlytics:2.10.1@aar') {
        transitive = true
    }

    implementation 'com.google.firebase:firebase-core:17.2.2'

    // https://github.com/ShawnLin013/NumberPicker
    implementation 'com.shawnlin:number-picker:2.4.8'

    // for unit tests
    testImplementation 'junit:junit:4.13'
}

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.2'

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 29

        versionCode 85
        versionName "1.1.5"

        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        }
    }
    flavorDimensions 'app', 'store'
    productFlavors {
        standard {
            dimension 'app'
            applicationId 'de.saschahlusiak.freebloks'
        }
        vip {
            dimension 'app'
            applicationId 'de.saschahlusiak.freebloksvip'
        }
        Google {
            dimension 'store'
            buildConfigField 'boolean', 'IS_AMAZON', 'false'
        }
        Amazon {
            dimension 'store'
            buildConfigField 'boolean', 'IS_AMAZON', 'true'
        }
    }
    externalNativeBuild {
         ndkBuild {
            path "src/main/jni/Android.mk"
         }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }

    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }
}

apply plugin: 'com.google.gms.google-services'
