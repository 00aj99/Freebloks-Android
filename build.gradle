buildscript {
    repositories {
        jcenter()
        google()
        maven {
            url 'https://maven.fabric.io/public'
        }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.4'
        classpath 'com.google.gms:google-services:4.0.1'
        classpath 'io.fabric.tools:gradle:1.25.1'
    }
}

repositories {
    jcenter()
    maven {
        url "https://maven.google.com"
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

dependencies {
    implementation 'com.github.clans:fab:1.6.4'

    // for Google+ integration. See "foss" branch without this dependency
    // https://developers.google.com/android/guides/releases
    //noinspection GradleCompatible
    implementation 'com.google.android.gms:play-services-games:15.0.1'
    implementation 'com.google.android.gms:play-services-plus:15.0.1'

    // for crash reporting through firebase. See "foss" branch without this dependency.
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.5@aar') {
        transitive = true
    }

    implementation 'com.google.firebase:firebase-core:16.0.3'

    // number picker for configure stones dialog
    implementation 'com.shawnlin:number-picker:2.4.5'
}

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.2'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 28

        versionCode 79
        versionName "1.0.9"

        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        }
    }
    flavorDimensions 'app', 'store'
    productFlavors {
        standard {
            dimension 'app'
            applicationId 'de.saschahlusiak.freebloks'
        }
        vip {
            dimension 'app'
            applicationId 'de.saschahlusiak.freebloksvip'
        }
        Google {
            dimension 'store'
            buildConfigField 'boolean', 'IS_AMAZON', 'false'
        }
        Amazon {
            dimension 'store'
            buildConfigField 'boolean', 'IS_AMAZON', 'true'
        }
    }
    externalNativeBuild {
         ndkBuild {
            path "jni/Android.mk"
         }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
}

apply plugin: 'com.google.gms.google-services'
