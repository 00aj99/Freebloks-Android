buildscript {
    repositories {
        jcenter()
        maven {
            url 'https://maven.fabric.io/public'
        }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.0'
        classpath 'com.google.gms:google-services:3.1.2'
        classpath 'io.fabric.tools:gradle:1.24.4'
    }
}

repositories {
    jcenter()
    maven {
        url "https://maven.google.com"
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

dependencies {
    compile 'com.github.clans:fab:1.6.4'

    // for Google+ integration. See "foss" branch without this dependency
    compile 'com.google.android.gms:play-services-games:11.8.0'
    compile 'com.google.android.gms:play-services-plus:11.8.0'

    // for crash reporting through firebase. See "foss" branch without this dependency.
    compile('com.crashlytics.sdk.android:crashlytics:2.7.1@aar') {
        transitive = true
    }
    compile 'com.google.firebase:firebase-core:11.8.0'
}

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.2'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 26

        versionCode 73
        versionName "1.0.3"

        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        }
    }
    flavorDimensions 'app', 'store'
    productFlavors {
        standard {
            dimension 'app'
            applicationId 'de.saschahlusiak.freebloks'
        }
        vip {
            dimension 'app'
            applicationId 'de.saschahlusiak.freebloksvip'
        }
        Google {
            dimension 'store'
            buildConfigField 'boolean', 'IS_AMAZON', 'false'
        }
        Amazon {
            dimension 'store'
            buildConfigField 'boolean', 'IS_AMAZON', 'true'
        }
    }
    externalNativeBuild {
         ndkBuild {
            path "jni/Android.mk"
         }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
}

apply plugin: 'com.google.gms.google-services'
