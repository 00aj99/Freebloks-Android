buildscript {
    ext.kotlin_version = '1.3.70'
    repositories {
        jcenter()
        google()
        maven {
            url 'https://maven.fabric.io/public'
        }
    }

    dependencies {
        // https://developer.android.com/studio/releases/gradle-plugin
        classpath 'com.android.tools.build:gradle:3.6.1'

        // kotlin
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        // https://developers.google.com/android/guides/releases
        classpath 'com.google.gms:google-services:4.3.3'

        // https://docs.fabric.io/android/changelog.html
        classpath 'io.fabric.tools:gradle:1.31.2'
    }
}

repositories {
    jcenter()
    maven {
        url "https://maven.google.com"
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // https://github.com/material-components/material-components-android
    // https://mvnrepository.com/artifact/com.google.android.material/material
    implementation 'com.google.android.material:material:1.2.0-alpha05'

    // https://developer.android.com/jetpack/androidx/releases/lifecycle
    def lifecycle_version = "2.2.0"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata:$lifecycle_version"

    // https://mvnrepository.com/artifact/androidx.preference/preference
    implementation 'androidx.preference:preference:1.1.0'

    // for Google+ integration. See "foss" branch without this dependency
    // https://developers.google.com/android/guides/releases
    implementation 'com.google.android.gms:play-services-games:19.0.0'
    implementation 'com.google.android.gms:play-services-auth:17.0.0'

    // https://docs.fabric.io/android/changelog.html#crashlytics
    implementation('com.crashlytics.sdk.android:crashlytics:2.10.1@aar') {
        transitive = true
    }

    // https://firebase.google.com/support/release-notes/android
    implementation 'com.google.firebase:firebase-analytics:17.2.3'

    // https://github.com/ShawnLin013/NumberPicker
    implementation 'com.shawnlin:number-picker:2.4.8'

    // for unit tests
    testImplementation 'junit:junit:4.13'
    testImplementation 'org.mockito:mockito-core:3.2.4'
}

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.3'

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29

        versionCode 94
        versionName "1.1.8"
    }
    flavorDimensions 'app', 'store'
    productFlavors {
        standard {
            dimension 'app'
            applicationId 'de.saschahlusiak.freebloks'
        }
        vip {
            dimension 'app'
            applicationId 'de.saschahlusiak.freebloksvip'
        }
        Google {
            dimension 'store'
            buildConfigField 'boolean', 'IS_AMAZON', 'false'
            buildConfigField 'String', 'APP_STORE_NAME', '\"Google Play Store\"'
        }
        Amazon {
            dimension 'store'
            buildConfigField 'boolean', 'IS_AMAZON', 'true'
            buildConfigField 'String', 'APP_STORE_NAME', '\"Amazon App Store\"'
        }
    }
    externalNativeBuild {
         ndkBuild {
            path "src/main/jni/Android.mk"
         }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }
}

apply plugin: 'com.google.gms.google-services'
